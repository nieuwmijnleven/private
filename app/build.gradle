/*
 * JADEx - Java Advanced Development Extension
 *
 * Copyright (C) 2026 Cheol Jeon <nieuwmijnleven@outlook.com>
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2 only,
 * as published by the Free Software Foundation.
 *
 * Alternatively, this software may be used under a commercial license
 * from Cheol Jeon.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 *
 * See the GNU General Public License version 2 for more details:
 * <https://www.gnu.org/licenses/old-licenses/gpl-2.0.html>.
 *
 * For commercial licensing, please contact <nieuwmijnleven@outlook.com>.
 *
 * Contributors to this project must sign a Contributor License Agreement (CLA)
 * granting Cheol Jeon the right to relicense their contributions under
 * a commercial license. See the CLA file in the project root for details.
 */

plugins {
    id 'java'
    id 'antlr'
}

group = 'jplus'
version = 'mvp'

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.12.0'
    implementation 'org.antlr:antlr4-runtime:4.12.0'
    implementation "org.ow2.asm:asm:9.9"
    implementation 'org.jspecify:jspecify:1.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.named('generateGrammarSource', AntlrTask).configure {
    arguments += ['-visitor', '-package', 'jplus.base']
    outputDirectory = file("src/main/antlr/generated-src/jplus/base")
}

sourceSets {
    main {
        java {
            srcDirs += "src/main/antlr/generated-src"
        }
    }
}

jar {
    archiveBaseName.set('jplus')
    archiveVersion.set('')
    manifest {
        attributes(
                'Main-Class' : 'jplus.main.Main'
        )
    }
}

task run(type: JavaExec) {
    group = 'application'
    description='Runs with arguments'
    mainClass.set('jplus.main.Main')
    classpath = sourceSets.main.runtimeClasspath
    if (project.hasProperty('args')) {
        args = project.property('args').split() as List<String>
    }
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(':app:build')) {
        tasks.named('generateGrammarSource').configure {
            enabled = false
        }
        tasks.named('generateTestGrammarSource').configure {
            enabled = false
        }
    } else if (taskGraph.hasTask(':app:run')) {
        tasks.named('generateGrammarSource').configure {
            enabled = false
        }
    } else if (taskGraph.hasTask(':app:test')) {
        tasks.named('generateGrammarSource').configure {
            enabled = false
        }
        tasks.named('generateTestGrammarSource').configure {
            enabled = false
        }
    }
}

test {
    useJUnitPlatform()
}
